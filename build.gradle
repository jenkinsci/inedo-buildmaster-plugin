/**
 * Gradle Jenkins JPI Plugin
 * 
 * https://wiki.jenkins-ci.org/display/JENKINS/Gradle+JPI+Plugin
 * https://github.com/jenkinsci/gradle-jpi-plugin
 *  
 * Gradle Tasks
 * ---------------
 * jpi:		Generates the JPI package
 * server: 	Run Jenkins in place with the plugin being developed
 *
 */

plugins {
 	id 'org.jenkins-ci.jpi' version '0.26.0'
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.inedo.buildmaster'
version = '1.7-SNAPSHOT'
description = 'Inedo BuildMaster Plugin'

ext {
	jenkinsVersion='2.60.3'
}

jenkinsPlugin {
    coreVersion = "$jenkinsVersion"
    displayName = 'Inedo BuildMaster Plugin.'
    url = 'http://wiki.jenkins-ci.org/display/JENKINS/Inedo+BuildMaster+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/inedo-buildmaster-plugin'
    compatibleSinceVersion = '1.7'
    developers {
        developer {
            id 'andrew-sumner'
            name 'Andrew Sumner'
            email 'andrew.sumner@xtra.co.nz'
        }
    }
}

repositories {
	maven { url 'http://repo.jenkins-ci.org/public' } // this was added to work around compile error "Could not find bootstrap-core-assets.jar (org.jenkins-ci.ui:bootstrap:1.3.2)"

    jcenter()
    
    maven { url 'https://jitpack.io' }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
	jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.15@jar'
    	
	compile ('com.github.concordion.cubano:cubano-httpeasy:HttpEasySecurity-SNAPSHOT') {
		exclude group: 'com.github.markusbernhardt', module: 'proxy-vole' 
	}
	
	testCompile 'com.jayway.jsonpath:json-path:2.3.0'
	testCompile 'junit:junit:4.8.2'
	testCompile 'org.mockito:mockito-core:2.18.3'
	testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.9'
	
	// TODO BuildMasterApiTest is using SimpleLogger - remove that and this
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'org.slf4j:slf4j-simple:1.7.21'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}
